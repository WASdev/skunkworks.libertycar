buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "org.hidetake:gradle-ssh-plugin:2.7.0"
    classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:1.0'
    classpath files('gradle/wlp-anttasks.jar')
  }
  
}

apply plugin: 'org.hidetake.ssh'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'liberty'

war {
  archiveName = 'LibertyCar.war'
}

liberty {
  installDir = "${buildDir}/wlp"
  install {
    version = '16.0.0_04'
  }

  packageLiberty {
    archive = "${buildDir}/LibertyCar.zip"
    include = "minify"
  }
}

remotes {
  carPiHost {
    host = '192.168.168.4'
    user = 'pi'
    password = 'raspberry'
    knownHosts = allowAnyHosts
  }
}

task uploadWar {
  doLast{
    ssh.run {
      session(remotes.carPiHost) {
        put from: "${war.archivePath}", into: '/home/pi/wlp/usr/servers/defaultServer/apps/LibertyCar.war'
      }
    }
  }
}

task uploadPackage {
  doLast{
    ssh.run {
      session(remotes.carPiHost) {
        put from: "${buildDir}/LibertyCar.zip", into: '/home/pi/LibertyCar.zip'
        execute "rm -rf /home/pi/wlp"
        execute "unzip /home/pi/LibertyCar.zip"
      }
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  providedCompile fileTree(dir: "${buildDir}/wlp/dev/api/spec", include: '*.jar')

  compile 'com.pi4j:pi4j-core:1.1'
  compile 'com.pi4j:pi4j-device:1.1'
  compile 'com.pi4j:pi4j-gpio-extension:1.1'
  compile 'com.pi4j:pi4j-service:1.1'
  compile 'com.google.code.gson:gson:2.8.0'

  testCompile 'junit:junit:4.12'
}

task copyUsrDir(type: Copy) {
  from 'src/main/wlp/usr'
  into "${buildDir}/wlp/usr"
}

task deployAppToAppsDir(type: Copy, dependsOn: ['assemble', 'copyUsrDir']) {
  from "${war.archivePath}"
  into "${buildDir}/wlp/usr/servers/defaultServer/apps"
}

build.dependsOn libertyPackage
libertyPackage.dependsOn deployAppToAppsDir
