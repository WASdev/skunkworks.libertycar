buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "org.hidetake:gradle-ssh-plugin:2.7.0"
    classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:1.0'
  }
}

apply from: 'gradleScripts/deploy.gradle'

apply plugin: 'war'
apply from: 'gradleScripts/eclipse.gradle'
apply from: 'gradleScripts/liberty.gradle'

repositories {
  mavenCentral()
  ivy {
    url 'http://code.jquery.com'
    layout 'pattern', {
      artifact '/[module]-[revision](.[classifier]).[ext]'
     }
   }
  ivy {
    url 'http://download.dojotoolkit.org'
    layout 'pattern', {
      artifact 'release-[revision]/[module]-release-[revision].[ext]'
    }
  }
}

configurations {
  adminWebLibs
  dojoJs
}

dependencies {
  adminWebLibs 'jquery:jquery:2.1.1@js'
  dojoJs 'dojo:dojo:1.7.4@zip'

  compile 'com.pi4j:pi4j-core:1.1'
  compile 'com.pi4j:pi4j-device:1.1'
  compile 'com.pi4j:pi4j-gpio-extension:1.1'
  compile('com.pi4j:pi4j-service:1.1') {
    exclude group: 'org.osgi'
  }
  compile 'com.google.code.gson:gson:2.8.0'

  testCompile 'junit:junit:4.12'
}

war {
  archiveName = 'LibertyCar.war'
  into("adminConfig/lib") {
    from {
      configurations.adminWebLibs
    }
  }
  into("remote") {
    from (zipTree(configurations.dojoJs.singleFile)) {
      include '**/dojo/**'
      includeEmptyDirs false
    }
  }
}

task deployAppToAppsDir(type: Copy, dependsOn: ['assemble', 'copyUsrDir', 'removeLooseConfig']) {
  from "${war.archivePath}"
  into "${liberty.userDir}/servers/${liberty.serverName}/apps"
}
libertyPackage.dependsOn deployAppToAppsDir

task removeAppFromAppsDir(type: Delete) {
  delete "${liberty.userDir}/servers/${liberty.serverName}/apps/${war.archiveName}"
}

task copyUsrDir(type: Copy) {
  from 'src/main/wlp/usr'
  into liberty.userDir
}

apply from: 'gradleScripts/looseConfig.gradle'
